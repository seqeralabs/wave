import java.time.OffsetDateTime
import java.time.format.DateTimeFormatter

plugins {
    id("groovy") 
    id("com.github.johnrengelman.shadow") version "7.1.1"
    id("io.micronaut.application") version "3.2.0"
    id "com.google.cloud.tools.jib" version "3.2.0"
}

version = "0.3"
group = "com.example"

repositories {
    mavenCentral()
}

compileGroovy.groovyOptions.configurationScript = file('gradle/config/groovyc.groovy')

dependencies {
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut.groovy:micronaut-runtime-groovy")
    implementation("jakarta.annotation:jakarta.annotation-api")
    implementation("io.micronaut:micronaut-validation")
    implementation "org.codehaus.groovy:groovy-json"
    implementation 'com.google.guava:guava:24.1.1-jre'
    compileOnly("io.micronaut:micronaut-http-validation")
    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("io.netty:netty-tcnative-boringssl-static:2.0.0.Final")
    testImplementation "org.apache.commons:commons-compress:1.21"
}


application {
    mainClass.set("io.seqera.App")
}
java {
    sourceCompatibility = JavaVersion.toVersion("11")
    targetCompatibility = JavaVersion.toVersion("11")
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("spock2")
    processing {
        incremental(true)
        annotations("com.example.*")
    }
}


//
// Build container image with JIB
// https://github.com/GoogleContainerTools/jib/tree/master/jib-gradle-plugin
//
jib {
    from {
        image = 'amazoncorretto:11.0.14'
        platforms {
            platform { architecture = 'amd64'; os = 'linux' }
        }
    }
    to {
        image = "pditommaso/tower-reg:$version"
    }
    container {
        creationTime = "${OffsetDateTime.now().format(DateTimeFormatter.ISO_DATE_TIME)}"
        filesModificationTime = "${OffsetDateTime.now().format(DateTimeFormatter.ISO_DATE_TIME)}"
        ports = ['9090']
        entrypoint = '/bin/sh'
        args = ['/reg.sh']  // copied from `src/main/jib` directory
    }
    extraDirectories {
        paths {
            path { from = file('build/layers'); into = '/build/layers' }
            path { from = file('src/main/jib'); }
        }
        permissions = ['/reg.sh': '755']
    }
}

